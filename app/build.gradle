plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.kindnesswand"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
        archivesBaseName = "kindnesswall-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "Long", "READ_TIMEOUT_SECOND", READ_TIMEOUT_SECOND
        buildConfigField "Long", "CONNECTION_TIMEOUT_SECOND", CONNECTION_TIMEOUT_SECOND

        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [isBranchSdkInTestMode: "true"]
        }
    }

    flavorDimensions "default"

    productFlavors {
        staging {
            versionNameSuffix 'Staging'
            buildConfigField "String", "URL_WEBAPI", "${STAGING_URL_WEBAPI_BASE_URL}"
        }

        production {
            buildConfigField "String", "URL_WEBAPI", "${PRODUCTION_URL_WEBAPI_BASE_URL}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }


    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }
}

dependencies {
    implementation project(path: ':spotlight')

    // Androidx Libs
    implementation libs.appcompat
    implementation libs.cardView
    implementation libs.recyclerView
    implementation libs.constraintLayout

    implementation libs.roomRuntime
    implementation libs.roomKtx
    kapt libs.roomCompiler

    implementation libs.androidXAnnotations
    implementation libs.fragmentKtx
    implementation libs.bundles.navigation

    implementation libs.lifecycleProcess
    implementation libs.lifecycleLivedataKtx
    implementation libs.lifecycleViewmodelKtx
    kapt libs.lifecycleCompiler

    implementation libs.swipeRefreshLayout
    implementation libs.coreKtx
    implementation libs.activityKtx
    implementation libs.browser
    implementation libs.bundles.compose
    implementation libs.composeMaterial

    implementation libs.composeThemeAdapter

    implementation libs.flexbox

    implementation libs.koin

    implementation libs.bundles.coroutines

    implementation libs.bundles.retrofit

    implementation libs.loggingInterceptor

    implementation libs.glide
    kapt libs.glideCompiler

    implementation libs.landscapistGlide

    implementation libs.bundles.firebase

    implementation libs.bundles.kotpref

    implementation libs.photoView

    implementation libs.indefinitePagerIndicator

    implementation libs.timeago

    implementation libs.swipeDecorator

    implementation libs.persianDate

    debugImplementation libs.chuckerLibrary
    releaseImplementation libs.chuckerLibraryNoOp

    implementation libs.timber

    implementation libs.bundles.accompanist
}

